buildscript {
	repositories {
		maven { url 'http://repo.spring.io/plugins-release' }
	}
	dependencies {
		classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
	}
}

configure(allprojects) {
	group 'org.saltframework'
	version '1.0.0.RELEASE'

	apply plugin: 'java'
	apply plugin: 'propdeps'
	apply plugin: 'pmd'

	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	repositories {
		mavenCentral()
	}

	ext {
		slf4jVersion = '1.7.12'
		junitVersion = '4.11'

		springVersion = '4.2.4.RELEASE'

		hibernateVersion = '5.1.0.Final'
		springDataJpaVersion = '1.10.1.RELEASE'

		mybatisVersion = '3.2.8'
		mybatisSpringVersion = '1.3.0'
	}

	configurations.all {
		exclude group: "commons-logging", module: "commons-logging"
	}

	dependencies {
		compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
		runtime('ch.qos.logback:logback-classic:1.1.7') {
			exclude group: 'org.slf4j', module: 'slf4j-api'
		}
		testCompile "junit:junit:$junitVersion"

		compile 'org.apache.commons:commons-lang3:3.4'

		compile 'com.h2database:h2:1.4.191'

		compile "org.springframework:spring-jdbc:${springVersion}",
				"org.springframework:spring-orm:${springVersion}",
				"org.springframework:spring-tx:${springVersion}",
				'commons-dbcp:commons-dbcp:1.4',
				'com.googlecode.log4jdbc:log4jdbc:1.2'

		compile "org.mybatis:mybatis:${mybatisVersion}",
				"org.mybatis:mybatis-spring:${mybatisSpringVersion}"

		compile "org.hibernate:hibernate-entitymanager:${hibernateVersion}",
				"org.springframework.data:spring-data-jpa:${springDataJpaVersion}"

		provided "javax.servlet:javax.servlet-api:3.0.1",
				"javax.servlet.jsp:jsp-api:2.1",
				"javax.el:javax.el-api:2.2.4"
	}
}

project(':salt-core') {
	group 'org.saltframework'
	version '1.0.0.RELEASE'

	dependencies {
		testCompile "org.springframework:spring-test:$springVersion"

		compile "org.springframework:spring-context:$springVersion"
	}

	jar {
		baseName = 'salt-core'
		version =  version
	}
}

configure(rootProject) {

	dependencies {
		compile project(':salt-core')
	}
}